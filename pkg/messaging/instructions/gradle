To use CI Fuzz and its dependencies you first must be able to access the CI repository.
For this, add the following repository settings to Gradle, e.g. to your ~/.gradle/gradle.properties file.

    CodeIntelligenceRepositoryUsername=<YOUR_USERNAME>
    CodeIntelligenceRepositoryPassword=<YOUR_TOKEN>

You can access your username and private token on the CI Download Portal Configuration page at https://downloads.code-intelligence.com/client.

To use the CI repository in your project, add it to your build.gradle file:

    repositories {
      maven {
        name = "CodeIntelligenceRepository"
        url = uri("https://gitlab.code-intelligence.com/api/v4/projects/89/packages/maven")
        credentials(PasswordCredentials.class)
        content {
          includeGroupByRegex("com\\.code-intelligence.*")
        }
      }
      mavenCentral()
    }

You also have to add the CI repository as an additional plugin repository to your settings.gradle file:

    pluginManagement {
      repositories {
        maven {
          name = "CodeIntelligenceRepository"
          url = uri("https://gitlab.code-intelligence.com/api/v4/projects/89/packages/maven")
          credentials(PasswordCredentials.class)
          content {
            includeGroupByRegex("com\\.code-intelligence.*")
          }
        }
        gradlePluginPortal()
      }
    }

To enable fuzz testing in your project, add our CI Fuzz Gradle plugin to your build.gradle:

    id "com.code-intelligence.cifuzz" version "1.11.0"

Fuzz test are executed with JUnit 5, which needs to be configured in your project.

    dependencies {
      testImplementation(platform("org.junit:junit-bom:5.10.0"))
      testImplementation('org.junit.jupiter:junit-jupiter')
    }

    test {
      useJUnitPlatform()
      testLogging {
        events("passed", "skipped", "failed")
      }
    }
